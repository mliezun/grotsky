expression      → assignment;
list            → "[" arguments? "]";
dictionary      → "{" dict_elements? "}";
dict_elements   → keyval ("," keyval)*;
keyval          → expression ":" expression;
assignment      → (call ".")? IDENTIFIER "=" assignment | logic_or;
logic_or        → logic_and ("or" logic_and)*;
logic_and       → equality ("and" equality)*;
equality        → comparison (("!=" | "==") comparison)*;
comparison      → addition ((">" | ">=" | "<" | "<=") addition)*;
addition        → multiplication (("-" | "+") multiplication)*;
multiplication  → power (("/" | "*") power)*;
power           → unary ("^" unary)*;
unary           → ("not" | "-") unary | call;
call            → primary ("(" arguments? ")" | "." IDENTIFIER | ("[" slice "]"))*;
arguments       → expression ("," expression)*;
slice           → (":" expression)
                | (":" expression ":" expression)
                | (":" ":" expression)
                | expression
                | (expression ":")
                | (expression ":" expression)
                | (expression ":" ":" expression)
                | (expression ":" expression ":" expression);
primary         → NUMBER
                | STRING
                | "false"
                | "true"
                | "nil"
                | IDENTIFIER
                | "(" expression ")"
                | fnAnon
                | list
                | dictionary;
fnAnon          → "fn" "(" parameters? ")" block;

program        → declaration* EOF;
declaration    → (classDecl | funDecl | varDecl | statement) NEWLINE;
classDecl      → "class" IDENTIFIER ( "<" IDENTIFIER )? "{" methodDecl* "}";
methodDecl     → "class"? function;
funDecl        → "fn" function ;
function       → IDENTIFIER "(" parameters? ")" (block | exprStmt) ;
parameters     → IDENTIFIER ( "," IDENTIFIER )* ;
varDecl        → "let" IDENTIFIER ("=" expression)?;
statement      → forStmt
                | ifStmt
                | returnStmt
                | whileStmt
                | exprStmt
                | block;
exprStmt       → expression;
forStmt        → "for"  (classicFor | newFor) statement;
classicFor     → (varDecl | exprStmt | ",") expression? "," expression?;
newFor         → IDENTIFIER ("," IDENTIFIER)? "in" expression;
ifStmt         → "if" expression block ("elif" expression block)* ("else" block)?;
returnStmt     → "return" expression?;
breakStmt      → "break";
continueStmt   → "continue";
whileStmt      → "while" expression statement;
block          → "{" declaration* "}";
